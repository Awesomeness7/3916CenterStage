//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


// Custom definitions may go here
plugins {
    id "com.github.node-gradle.node" version "2.2.4"
}
// Include common definitions from above.
apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

android {
    namespace = 'org.firstinspires.ftc.teamcode'


    packagingOptions {
        jniLibs.useLegacyPackaging true
    }

}

node {
    version = '18.9.1'
    download = true
    nodeModulesDir = file("${project.projectDir.parent}/web")
}

pnpm_build.dependsOn pnpm_install

task cleanWebAssets(type: Delete) {
    delete "${android.sourceSets.getByName("main").assets.srcDirs[0]}/web"
}

clean.dependsOn cleanWebAssets

task copyWebAssets(type: Copy) {
    from "${project.projectDir.parent}/web/dist"
    into "${android.sourceSets.getByName("main").assets.srcDirs[0]}/web"
}

copyWebAssets.dependsOn cleanWebAssets
copyWebAssets.dependsOn yarn_build

android.libraryVariants.all { variant ->
    variant.preBuildProvider.get().dependsOn copyWebAssets
}

repositories {
    maven {
        url = 'https://maven.brott.dev/'
    }
}

dependencies {
    implementation project(':FtcRobotController')
    annotationProcessor files('lib/OpModeAnnotationProcessor.jar')

    implementation 'org.ftclib.ftclib:core:2.1.1'

    implementation "com.acmerobotics.roadrunner:ftc:0.1.6"
    implementation "com.acmerobotics.dashboard:dashboard:0.4.12"
    implementation "org.tomlj:tomlj:1.1.0"
}
